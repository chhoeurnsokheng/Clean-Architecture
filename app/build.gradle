apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'


def major = 1, minor = 0, patch = 0, build = 1

if (major > 209) throw new GradleException("Major version ${major} exceeds limit")
if (minor > 99) throw new GradleException("Minor version ${minor} exceeds limit")
if (patch > 99) throw new GradleException("Patch version ${patch} exceeds limit")
if (build > 999) throw new GradleException("Build version ${patch} exceeds limit")

def code = (major * 10000000) + (minor * 100000) + (patch * 1000) + build
def name = "$major.$minor.$patch.$build"

android {
    compileSdk 30
    defaultConfig {
        applicationId "com.example.myapplication"
        minSdk 21
        targetSdk 30
        versionCode code
        versionName name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile file("$rootProject.projectDir/Keystore/debug.jks")
            storePassword "123456"
            keyAlias "debug"
            keyPassword "123456"
        }
        stage {
            storeFile file("$rootProject.projectDir/Keystore/debug.jks")
            storePassword "123456"
            keyAlias "debug"
            keyPassword "123456"
        }
        release {
            storeFile file("$rootProject.projectDir/Keystore/release.jks")
            storePassword "123456"
            keyAlias "release"
            keyPassword "123456"
        }
    }

    buildTypes {
        debug{
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix ".$build"
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        stage{
            debuggable false
            minifyEnabled false
            applicationIdSuffix ".stage"
            versionNameSuffix ".$build"
            signingConfig signingConfigs.stage
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures{
        viewBinding =true
    }
}


configurations {
    ktlint
}
//ktlint
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

dependencies {
    def version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$version"
    // or lifeycle-compiler if not using Java 8
    implementation "androidx.activity:activity-ktx:1.3.1"
    implementation "androidx.fragment:fragment-ktx:1.3.6"
    //ViewModel and livedata
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    //Retrofit
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    //Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    //hilt
    implementation "com.google.dagger:hilt-android:2.38.1"
    kapt"com.google.dagger:hilt-android-compiler:2.38.1"
    //ktlint
    ktlint "com.pinterest:ktlint:0.42.1"

    implementation "androidx.hilt:hilt-navigation-fragment:1.0.0"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
